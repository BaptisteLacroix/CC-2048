class Tile {
  // attributes
  int value;
  PVector position;
  PVector pixelPos;//top left
  boolean alreadyIncreased = false;
  boolean moving = false;

  color colour;
  PVector positionTo;
  PVector pixelPosTo;


  boolean deathOnImpact =false;
  
  HashMap<Integer, Integer> colorMap;

  Tile(int x, int y) {
    // constructor
    if (random(1)< 0.1) {
      value = 4;
    } else {
      value =2;
    }
    
    colorMap = new HashMap<Integer, Integer>();
    colorMap.put(2,#eee4da);
    colorMap.put(4,#ede0c8);
    colorMap.put(8,#f2b179);
    colorMap.put(16,#f59563);
    colorMap.put(32,#f67c5f);
    colorMap.put(64,#f65e3b);
    colorMap.put(128,#edcf72);
    colorMap.put(256,#edcc61);
    colorMap.put(512,#edc850);
    colorMap.put(1024,#C6754A);
    
    position = new PVector(x, y);
    positionTo = new PVector(x, y);

    pixelPos = new PVector(xoffset +x*200 + (x+1) *10, yoffset + y*200 + (y+1) *10);
    pixelPosTo = new PVector(xoffset + x*200 + (x+1) *10, yoffset + y*200 + (y+1) *10);

    setColour();
  }

  // method
  void show() {
    if (!deathOnImpact || moving) {
      fill(colour);
      noStroke();
      rect(pixelPos.x, pixelPos.y, 200, 200);
      if (value < 8) {
        fill(40);
      } else {
        fill(240);
      }
      textAlign(CENTER, CENTER);
      textSize(50);
      text(value, pixelPos.x+100, pixelPos.y+100);
    }
  }

  void moveTo(PVector to) {
    positionTo = new PVector(to.x, to.y);
    pixelPosTo = new PVector(xoffset +to.x*200 + (to.x+1) *10, yoffset +to.y*200 + (to.y+1) *10);
    moving = true;
  }
  void moveToNow(PVector to) {
    position = new PVector(to.x, to.y);
    pixelPos = new PVector(xoffset +to.x*200 + (to.x+1) *10, yoffset +to.y*200 + (to.y+1) *10);
  }

  void move(int speed) {
    if (moving) {
      if (!teleport && dist(pixelPos.x, pixelPos.y, pixelPosTo.x, pixelPosTo.y) > speed) {
        PVector MoveDirection = new PVector(positionTo.x - position.x, positionTo.y - position.y); 
        MoveDirection.normalize();
        MoveDirection.mult(speed);
        pixelPos.add(MoveDirection);
      } else {
        moving = false;
        pixelPos = new PVector(pixelPosTo.x, pixelPosTo.y);
        position = new PVector(positionTo.x, positionTo.y);
      }
    }
  }
  
  void setColour() {
    colour = colorMap.get(value);
  }

  Tile clone() {
    Tile clone = new Tile(floor(position.x), floor(position.y));
    clone.value = value;
    clone.setColour();

    return clone;
  }
}
